@use Abstract.

Abstract : Tone (aka Tones) {
	% A Tone is an object which plays a constant tone.  You can set 
	% the volume, frequency and left-right balance of the tone.  Creating
	% the object will automatically start playing the tone.  Deleting the 
	% object will cause the tone to stop playing.
	% <p>
	% A tone can also be effectively stopped by setting its volume to 0.

    + variables:
        tonePointer (pointer).
        
    + to init:
        tonePointer = newSinewave(0).
        
    + to set-balance to newBalance (double):
		% Sets the left/right balance of the sinewave to newBalance, a 
		% value between 0 and 1.  .5 is perfectly balanced, while 1.0
		% plays the tone completely on the right channel and 0.0 plays
		% the tone entirely on the left channel.

		if tonePointer: setBalance(tonePointer, newBalance).

    + to set-volume to newVolume (double):
		% Sets the volume of the tone to newVolume.  Volume is specified
		% on a scale from 0.0 to 1.0.

		if newVolume > 1.0: newVolume = 1.0.
		else if newVolume < 0.0: newVolume = 0.0.

		if tonePointer: setVolume(tonePointer, newVolume).

    + to set-frequency to newFrequency (double):
		% Sets the frequency of the tone to newFrequency.

		if tonePointer: setFrequency(tonePointer, newFrequency).

	+ to destroy:
		if tonePointer: freeSinewave(tonePointer).
}
